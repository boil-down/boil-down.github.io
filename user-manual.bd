% v:beta
% author: Jan Bernitt

***{article}[a4 bc-white]

                         boildown \\ User Manual     [<>]

1 Principles

Depending on your knowledge and understanding of programming, interpreters and
markup languages the following list of principle might help you a lot in 
understanding how the markup works. 
Otherwise just skip and ignore them and learn from the examples below.

* no escape to a less constraint language (like HTML)
* aims to enable readable ASCII documents
* _but_: no multiplicity in expressive possibilities (bears complexity)
* interpretation is line wise (only)
* semantics are never altered by subsequential lines
* main loop determines where a block starts
* blocks determine where they end 
* blocks determine if and where to apply inline markup
* inline markup cannot span multiple lines
* not characters but sections of (inline) text are escaped 
* inline markup is short rather than secured against unwanted matches 
  (as it can be escaped easily)
* empty lines only start/end paragraphs (but have no other semantic)
* block nesting is done by consistent indentation
* block start marks are left aligned (after potential indent; except separators)
* start and end marks of inline markups are identical (escaping is an exception)
* inline marks do not use combinations of different characters

2 Text Formatting

Semantic markups use single character marks:
? _emphasis_

? *strong emphasis*

? "cite"

?  /alternative voice/ 

?  -inaccurate text- 

?  :definition: 

? .ABBR.

? `code`, ```

? @user input@, @@@

? !annotated!{spelling}
? !annotated!{grammar}
? !annotated!{bc-warning}

...one or more when nesting makes sense...

? base^superscript^
? ^a^^b^^ ^
? ^^b^a^ ^^

? base~subscript~
? ~a~~b~~ ~
? ~~b~a~~ ~

? a 'quote',
? a '' 'quote' within a quote'',
? a ' ''quote'' within a quote'

Stylistic or editorial markups use two character marks:
? ``monospace``

? ==small caps==

? __underlined__

? ::coloured::{orange}
? ::coloured::{#7FDBFF}

? --deleted--

? ++inserted++


3 Inline Escaping

Text in-between double curly brackets `{{{{}}`...`{{}}}}` is not interpreted. 
? {{*plain* _text_}}

Escaped sections can be contained within inline elements.
? `{{*plain* _text_}}`
? *plain {{_text_}}*


4 Special Characters
 
Manual line breaks are forced using `\\`:
? manual line \\ break
Otherwise new paragraphs start after blank lines or within a new block.

 For manual page break a anterisk `*` is added to a usual line break:
? manual page \\* break

Line breaks opportunities can be marked using `\-`:
? along\-word\-that\-might\-break\-somewhere

Special punctuation marks:
? ...

? x --- y

? x -- y

HTML-entities can be used by name:
? &amp;loz;


5 Links

Links are given in pairs of `[[_URL_ _label_]]`:
? [[/relative/url label]]

? [[http://dilbert.com/ dilbert]]

Links without label are just given plain but they have to start with `http/s` or `www` to be auto-linked:

? http://dilbert.com/
? www.youtube.com

Link creation can be escaped as usual:

? {{http://dilbert.com/}}

6 Includes

Links can also be used to include other boildown documents or slices of them
into another document. Such links are marked with an extra `*`:

? [[part.bd an include]]*

Like everything else the include will be done by the client. 
 A client may choose to not do includes in which case the link stays an ordinary 
link with an addition CSS class `bd-include`.

Includes can also be done for a range of lines, using `#L_{from}_-_{to}_`:

? [[part.bd#L1-L2 range include]]*

TODO includes by heading section range (I-V)  

7 Editing

? ---
? deleted content
? ---

? +++
? added content
? +++

Using option to set date of edit:

? +++{2016-04-01}
? Added 1.of April
? +++


8 Headings

 A line start starts with at least 3 spaces or tabs is a title:
?    Title
?      Title

Parts, chapters and sections are numbered 
(and must be followed by a space and not end with a dot):
? 1 Chapter 1
? 2 Chapter 2
? 2.1 Section Foo
? 2.2 Section Bar

Or when using parts:
? I Part 1
? I.1 Chapter 1
? I.1.1 Section 1 in Chapter 1

The section numbers become anchors.

An appendix can used upper case letters that might be followed by a `)` but
must be followed by a space afterwards.
? A) Appendix A

 A unnumbered index like a bibliography can use `#` followed by a space:
? # Bibliography

To specify the name of a headings anchor use `{_..._}`:
? 1.1 Heading {anchorname}

Headings are usual block elements to options can be used:

? 1 Chapter 1 [=>]

9 Quoting

Simple block quotes start with `> ` (space!): 
? > a quote using
? > multiple lines

? > > double quoting...

Advanced block quotes `>>>` can be used with options, e.g. to preserve line wrapping:
? >>>[\\]
? a quote using
? multiple lines
? wrapping like 
? source
? >>>

Using option to specify citation:
? >>>{Me}
? Simple new stupidity
? >>>

Using minipages to position a cite footer.
? > [...] we fell that [...] 
? > kids would be lucky, 
? > if they are as well educated 
? > as we are, 
? > which is a terrible crime, 
? > because we ain't educated at all. 
? > We're only beginning to realize 
? > how little we know 
? > about the nature of reality.
? > ***{footer}[>]
? >  --- Russell Ackoff, 
? > "Speech on Systems"
? > ***


10 Separator Lines

Any line that does start with whitespace and that just contains whitespace and 
at least 3 minus, plus or asterisks symbols is a separator line.

?      --------------

?  + - + - + - + - +

?  - - - - - - - - -

?    * * *   

?  **  * ** * ** * **

Separator lines are empty but proper blocks, styles can be used:

?  -------------[40%][<=][#000000]


11 Lists

Ordered lists start at the number of the first item given.
The number or letter has to followed by a dot and a space `n. `:
? 10. one
? #. two
? 4. three

`#` can be used instead of an actual number or letter:
? #. one
? #. two

Equally lists can use lower-case letters:
? c. cesar
? d. delta
? #. eta

Or upper-case letters:
? A. Alpha
? #. Beta

And even roman numbers work, but here the lists have to start with `I.`

? I. one
? #. two
? #. three

In case of letters a specified item cannot have multiple letters. 
Instead the placeholder `#.` should be used.

Asterisk `*` followed by a space are used for bullet lists.

? * one
? * other

Any content can be nested within an item. 
All content that should be nested has to be indented by 2 spaces and/or tabs
per level.

? 1. one
?   further text in one
?         ----
?   and further text
? 
?   also with a new paragraph
? #. second item
?   a. another nested list
?   #. with items
?     and nested content
?     ```
?     all content is indented
?     blocks as well
?     ```


12 Listings

? ```
? 1 * 2 * 3 = 6
? ```

Highlight lines...
? ```*
? ! 1 * 2 * 3 = 6
? x 1 + 2 + 3 = x6x
? ```

Draw attention to keywords using the option `{_wordlist_}`:
? ```{key word}
? Highlights the key words
? word by word
? ```

Give samples
? ~~~
? A sample 
? block
? ~~~


13 Images

Images are given in format `( _URL_ _[title]_ )`:
? ( bee.png a bee )

They are blocks with [[#options options]], here to make image float right:
? ( bee.png a bee )[=>]

 A gallery or _figure_ is done by a sequence of images, the caption `((`...`))` is optional.
? (( bees        ))[<>]
? ( bee.png bee 1 )
? ( bee.png bee 2 )
? ( bee.png bee 3 )

If the caption isn't stated first, the caption goes below the images:
? ( bee.png  )
? (( a *bee* ))


14 Tables

? :: green colors ::['][<=>]
? :.........:[#4CAF50]
? : Green   :*{2}
? :.........:[#4CAF50]
? : 500     :
? : #4CAF50 :
? :.........:[#E8F5E9]
? : 50      :
? : #E8F5E9 :
? :.........:[#C8E6C9]
? : 100     :
? : #C8E6C9 :
? :.........:[#A5D6A7]
? : 200     :
? : #A5D6A7 :
? :.........:[#81C784]
? : 300     :
? : #81C784 :
? :.........:[#66BB6A]
? : 400     :
? : #66BB6A :
? :.........:[#4CAF50]
? : 500     :
? : #4CAF50 :
? :.........:[#43A047]
? : 600     :
? : #43A047 :
? :.........:[#388E3C]
? : 700     :
? : #388E3C :
? :.........:[#2E7D32]
? : 800     :
? : #2E7D32 :
? :.........:[#1B5E20]
? : 900     :
? : #1B5E20 :
? :.........:


15 Minipages

? ***{aside}[<>][#F44336]
? ****[50%][#2196F3][<=]
? another page within a page
? ****
? ****[50%][#4CAF50][=>]
? and another one besides
? ****
? and back in the outer minipage
? ***


16 Documentation

? ? _shows_ an example

? ! _shows_ the HTML


17 Page

As blocks cope with missing end blocks one can start a document with a minipage
to apply some _global_ options.

? ***[pad3]
